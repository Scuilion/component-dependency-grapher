apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}
tasks.withType(JavaCompile){
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.neo4j:neo4j:2.1.2'
    compile 'com.google.guava:guava:17.0'
    
    testCompile 'org.neo4j:neo4j-kernel:2.1.2:tests@jar'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

//Add funtional test
configurations {
    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime

    functionalTestCompile.extendsFrom compile
    functionalTestRuntime.extendsFrom runtime
}

sourceSets {
    functional {
        java {
            srcDir 'src/functional/java'
        }
        resources {
            srcDir 'src/functional/resources'
        }
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
    }
}

task functionalTest(type:Test){
    testClassesDir = sourceSets.functional.output.classesDir
    classpath = sourceSets.functional.runtimeClasspath + sourceSets.main.output + configurations.functionalTestCompile + sourceSets.test.output
}
